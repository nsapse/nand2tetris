// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:

    // zx
    And16(a=false, b=x, out=ZXMuxB);
    Mux16(a=x, b=ZXMuxB, sel=zx, out=NotBusX);

    // nx
    Not16(in=NotBusX, out=NXMuxB);
    Mux16(a=NotBusX, b=NXMuxB, sel=nx, out=FBusX);
    
    // zy
    And16(a=false, b=y, out=ZYMuxB);
    Mux16(a=y, b=ZYMuxB, sel=zy, out=NotBusY);

    // ny
    Not16(in=NotBusY, out=NYMuxB);
    Mux16(a=NotBusY, b=NYMuxB, sel=ny, out=FBusY);

    // f
    And16(a=FBusX, b=FBusY, out=FMuxA);
    Add16(a=FBusX, b=FBusY, out=FMuxB);
    Mux16(a=FMuxA, b=FMuxB, sel=f, out=NOBus);

    // no
    Not16(in=NOBus, out=NOMuxB);
    Mux16(a=NOBus, b=NOMuxB, sel=no, out=out, out[0..7]=leftOut, out[8..15]=rightOut, out[15]=SignBit);

    // zr
    Or8Way(in=leftOut, out=OrA);
    Or8Way(in=rightOut, out=OrB);
    Or(a=OrA, b=OrB, out=negateZR);
    Not(in=negateZR, out=zr);

    // ng
    Or(a=false, b=SignBit, out=ng);

    }
